; For every nonnegative integer, machine computes floor(sqrt(n))
; It uses fact that n^2=1+3+5+...+(2n-1)

; Short setup
; On the left there is current addend, now it is 1
0 1 1 l 0
0 _ _ l 1
1 _ 1 r 5
; We mark exactly that many 1's from input as there is in addend
2 _ 1 l 6
2 x x l 2
2 1 x r 3
3 x x r 3
3 _ _ r 4
4 x x r 4
4 1 x l 5
4 _ _ l 8
5 x x l 5
5 _ _ l 2
; If we used whole addend, we increase it by 2
6 _ 1 r 7
7 1 1 r 7
7 x 1 r 7
7 _ _ l 2
; If we marked whole input, we remove it, leaving only output
8 x _ l 8
8 _ 1 l 9
; At this moment, addend is exactly 2n+1, where n is wanted output
9 _ _ l 9
9 x _ l 10
9 1 _ l 10
10 x _ r 11
10 1 _ r 11
10 _ _ r 14
11 _ _ r 11
11 1 1 r 12
12 1 1 r 12
12 _ 1 l 13
13 1 1 l 13
13 _ _ l 9
; Now there is n+1 left on tape. We remove one 1 and we're done
14 _ _ r 14
14 1 _ r halt